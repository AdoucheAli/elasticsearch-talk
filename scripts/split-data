#!/usr/bin/env groovy

import groovy.json.JsonSlurper
import groovy.json.JsonBuilder

String userHome = System.getProperty('user.home')

// remove any old files starting with `location_`
new File("${userHome}/data/docs").eachFile { oldFile ->
	oldFile.delete()
}

println "Reading wikipedia locations file"
// read in the long json string
def jsonStr = new File("${userHome}/data/original/wikipedia_locations.json").text
// slurp the json into objects
def slurper = new JsonSlurper()
def json = slurper.parseText(jsonStr)

def bulkFile = new File("${userHome}/data/bulk/locations.json")

Date now = new Date()
Random r = new Random()
Integer secondsInYear = 365L * 24 * 60 * 60
// for each location under the `results` property
json.results.each { location ->
	println "- Writing '${location.wikipedia_id}'"
	// calculate a slug, replacing whitespace and punctuation with underscores
	String slug = location.wikipedia_id.replaceAll(/\W+/, '_').toLowerCase()
	// construct a filename using the slug
	location._id = slug
	// pick a random last updated time between now and a year ago
	location.lastUpdated = new Date(now.time - new Long(r.nextInt(secondsInYear)) * 1000L)

	// write the location json file to the calculated filename
	def docJson = new JsonBuilder(location)
	new File("${userHome}/data/docs/${slug}") << docJson.toPrettyString()

	def bulkJson = new JsonBuilder([index: [_id: slug]])

	bulkFile << bulkJson.toString() << '\n'
	bulkFile << docJson.toString() << '\n'
}